* Dipole moments for backbone atoms of each residue
* in a protein 
*

prnlev  5
bomlev -2

set seg PROA

! read in force field
stream toppar_drude/toppar_drude_master_protein_2019g.str
stream toppar_drude/toppar_drude_lipid_2017c.str

!Read PSF and initial/reference coordinates
read psf xplor card name walp23_popc.drude.xplor.psf
read coor card name ref.crd
coor copy comp

! open trajectory
open read  unit 13 file name full_fix.dcd
traj query unit 13

! set up some convenient definitions
define d_peptide1 sele segid PROA .and. (type C .or. type O .or. type LPOA .or. type LPOB .or. -
                      type DC .or. type DO) show end
define d_peptide2 sele segid PROA .and. (type CA .or. type HA# .or. type N .or. type HN .or. -
                      type DCA .or. type DN) show end
! if terminal residue has CT2 or CT3 cap
define d_peptide3 sele segid PROA .and. (type CAT .or. type HTC# .or. type NT .or. type HNT .or. -
                      type DCAT .or. type DNT) show end

set i 1
set lastres 23 
label loop1

    ! set up trajectory for reading
    traj firstu 13 nunit 1 begin 0 skip 1 stop ?nfile 

    calc j = @i + 1

    ! open output file
    open write unit 11 card name dipole.@i.components.bb.dat

    ! loop over trajectory frames
    set x 1
    label trajloop
        traj read
        calc t = @x * 0.01
        if @i .lt. @lastres then
            coor dipo sele (resi @i .and. d_peptide1) .or. (resi @j .and. d_peptide2) end 
        else
            coor dipo sele (resi @i .and. d_peptide1) .or. (resi @i .and. d_peptide3) end 
        endif

        write title unit 11
        *@t ?rdip ?xdip ?ydip ?zdip 
        *
        incr x by 1
    if @x .le. ?nfile goto trajloop

    rewind unit 13
    incr i by 1

if i .le. @lastres goto loop1

stop
