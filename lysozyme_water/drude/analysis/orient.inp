* Re-image trajectory 
*

dimens chsize 1000000

iofo exte

!Read topology and parameters (aka force field)
stream toppar_drude/toppar_drude_master_protein_2019g.str

!Read PSF and initial/reference coordinates
read psf xplor card name 1aki.drude.xplor.psf
read coor pdb resid name 1aki.drude.0.pdb 
coor copy comp

coor print

! Setup box and imaging 
set box 67.52483467
crystal define cubic @box @box @box 90. 90. 90.
crystal build noper 0 cutoff 10.
image byseg xcen 0. ycen 0. zcen 0. sele segid PROA end
image byres xcen 0. ycen 0. zcen 0. sele .not. segid PROA end

! Open file units for input and output trajectory
open read  unit 13 file name full.dcd
open write unit 15 file name full_nowat.dcd

!"Merge" to create a new trajectory
merge firstu 13 nunit 1 output 15 sele segid PROA end -
    recenter orient sele segid PROA .and. type CA end

close unit 13
close unit 15

! make matching PSF and reference PDB
bomlev -2
dele atom sele .not. segid PROA end

open write unit 30 card name 1aki.drude.nowat.xplor.psf
write psf xplor card unit 30
close unit 30

open write unit 31 card name 1aki.drude.nowat.0.pdb
write coor unit 31 pdb
close unit 31

stop

