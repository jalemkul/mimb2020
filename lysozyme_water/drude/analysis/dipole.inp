* Dipole moments for backbone atoms of each residue
* in a protein 
*

prnlev  5
bomlev -2

set seg PROA

! read in force field
stream toppar_drude/toppar_drude_master_protein_2019g.str

!Read PSF and initial/reference coordinates
read psf xplor card name 1aki.drude.nowat.xplor.psf
read coor pdb resid name 1aki.drude.nowat.0.pdb 
coor copy comp

! set up some convenient definitions
define d_peptide1 sele segid PROA .and. (type C .or. type O .or. type LPOA .or. type LPOB .or. -
                      type DC .or. type DO) show end
define d_peptide2 sele segid PROA .and. (type CA .or. type HA# .or. type N .or. type HN .or. -
                      type DCA .or. type DN) show end
! backbone vs. sidechain
define backbone sele type C .or. type O .or. type LPOA .or. type LPOB .or. -
                     type DC .or. type DO .or. type CA .or. type HA# .or. type N .or. -
                     type HN .or. type DCA .or. type DN .or. type HT# .or. -
                     type CAT .or. type HTC# .or. type NT .or. type HNT .or. -
                     type DCAT .or. type DNT .or. type OT# .or. type DOT# .or. type LPT# show end
define sidechain sele segid PROA .and. .not. backbone show end

! open trajectory
open read  unit 13 file name full_nowat.dcd
traj query unit 13

set i 1
set lastres 129 
label loop1

    ! set up trajectory for reading
    traj firstu 13 nunit 1 begin 0 skip 1 stop ?nfile 

    calc j = @i + 1

    ! open output files
    if @i .ne. @lastres then
        open write unit 11 card name dipole.@i.@j.bb.dat
    endif

    ! no side chain for Gly
    coor stat sele segid PROA .and. resid @i end
    if ?SELRESN .ne. GLY then
        open write unit 12 card name dipole.@i.sc.dat
    endif

    ! loop over trajectory frames
    set x 1
    label trajloop
        traj read
        calc t = @x * 0.01
        ! last residue cannot be calculated
        if @i .ne. @lastres then
            coor dipo mass sele (resi @i .and. d_peptide1) .or. (resi @j .and. d_peptide2) end 

            write title unit 11
            *@t ?rdip
            *
        endif

        ! non-Gly side chains can be calculated
        if ?SELRESN .ne. GLY then
            coor dipo mass sele resi @i .and. sidechain end
            write title unit 12
            *@t ?rdip
            *
        endif

        incr x by 1
    if @x .le. ?nfile goto trajloop

    rewind unit 13
    incr i by 1

if i .lt. @lastres goto loop1

stop
